#!/bin/awk -f
BEGIN {
  printf "(define (problem foo) :domain #'blocks-domain :inits (\n"
}
/^$/ {
  mode++
  block = 0
  next
}
mode==0 {
  n = $1
  next
}
mode==1 {
  block++
  if ($1 == -1)
    printf "(on %d table)\n", block
  else {
    printf "(on %d %d)\n", block, $1 + 1
    covered[$1 + 1] = 1
  }
  next
}
mode==2 {
  if (block == 0) {
    for (i = 1; i <= n; i++)
      if (!covered[i])
        printf "(clear %d)\n", i
    printf ") :goal (and\n"
    for (i = 1; i <= n; i++)
      covered[i] = 0
  }
  block++
  if ($1 == -1)
    printf "(on %d table)\n", block
  else {
    printf "(on %d %d)\n", block, $1 + 1
    covered[$1 + 1] = 1
  }
  next
}
END {
    for (i = 1; i <= n; i++)
      if (!covered[i])
        printf "(clear %d)\n", i
    printf "))\n"
}
